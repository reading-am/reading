class ø.Models.Post extends ø.Backbone.Model
  type: "Post"

  initialize: ->
    @bind "change:id", @keep_fresh
    @bind "destroy", => @intervals "clear"

  validate: (attr) ->
    if !attr.url || attr.url is "about:blank"
      return errors.general

  short_url: ->
    "<%= SHORT_DOMAIN %>/p/#{base58.encode(@id)}"

  # update the date_created every 15 seconds ala chartbeat
  # consider not doing this for context-menu posts
  keep_fresh: ->
    loading = false
    @intervals "add", 3, =>
      now = new Date
      if win_focus and not loading and now - @get("updated_at") >= 15000
        loading = true
        @save "updated_at", now, success: -> loading = false

class ø.Collections.Posts extends ø.Backbone.Collection
  type: "Posts"
  model: ø.Models.Post
