define [
  "backbone"
  "app/init"
  "app/constants"
  "app/collections/users"
  "app/collections/comments"
], (Backbone, App, Constants) ->

  class Page extends Backbone.Model
    type: "Page"

    initialize: ->
      @has_many "Users"
      @has_many "Posts"
      @has_many "Comments"

  Page::meta_tag_namespaces = <%= Page::META_TAG_NAMESPACES.to_json %>

  Page::parse_canonical = (canonical, host, protocol) ->
    domain = host.split(".")
    domain = "#{domain[domain.length-2]}.#{domain[domain.length-1]}"

    # twitter doesn't update the canonical on push state
    excluded_domains = ["twitter.com"]

    # this has a Ruby companion in app/models/page.rb#remote_canonical()
    if !canonical or
    excluded_domains.indexOf(domain) > -1
      canonical = false
    # protocol relative url
    else if canonical[0..1] is "//"
      canonical = "#{protocol}#{canonical}"
    # relative url
    else if canonical[0] is "/"
      canonical = "#{protocol}//#{host}#{canonical}"
    # sniff test for mangled urls
    else if canonical.indexOf("//") is -1 or
    # sniff test for urls on a different root domain
    canonical.indexOf(domain) is -1
      canonical = false

    canonical

  Page::parse_url = (url) ->
    regex = new RegExp "(?:https?:\/\/#{Constants.domain.replace(/\./g,"\\.")}\/(?:(?:p|t)\/[^\/]+\/)*)?(.+)"
    reg_url = regex.exec(url)[1]

    # don't lob off reading.am if they're reading a Reading page (a user profile, for instance)
    url = reg_url unless reg_url.indexOf('.') is -1
    url = "http://#{url}" if url.indexOf('://') is -1

    return url

  App.Models.Page = Page
  return Page
