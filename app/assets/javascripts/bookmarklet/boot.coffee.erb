load_style = (path) ->
  style = document.createElement("link")
  style.type = "text/css"
  style.rel  = "stylesheet"
  style.href = path
  document.getElementsByTagName("head")[0].appendChild(style)

load_style "//<%= DOMAIN %>/assets/bookmarklet/loader.css"
load_style "//<%= DOMAIN %>/assets/jquery/mentionsInput.css"

curl [
  "jquery"
  "underscore"
  "backbone"
  "app/constants"
  "app/models/post"
  "app/views/bookmarklet/app"
  "app/collections/pages"
  "app/collections/providers"
], ($, _, Backbone, Constants, Post, AppView) ->

  #-----------------------------------------
  # Config Vars

  reading.ready = false
  on_reading = window.location.host.indexOf("reading.am") is 0 or
               window.location.host.indexOf("staging.reading.am") is 0 or
               window.location.host.indexOf("0.0.0.0") is 0
  token = reading.token ? null
  platform = reading.platform ? (if on_reading then "redirect" else "bookmarklet")
  version = reading.version ? "0.0.0"

  latest_versions =
    bookmarklet: "<%= BOOKMARKLET_VERSION %>"

  #-----------------------------------------
  # Helpers

  parse_url = ->
    url = window.location.href.split(window.location.host)[1].substring(1)
    while url.substring(0, 2) is 't/' or url.substring(0, 2) is 'p/'
      url = url.substring(url.indexOf('/', 2) + 1)
    url = "http://#{url}" if url.indexOf('://') is -1
    url

  #######
  # API #
  #######
  class API

  API::call = (method, params) ->
      data = if params.data? then params.data else {}
      data.token = token if token

      complete = if params.complete? then params.complete else ->
      success = if params.success? then params.success else log
      error = if params.error? then params.error else (jqXHR, textStatus, errorThrown) ->
        log jqXHR, textStatus, errorThrown
        switch errorThrown
          when "Bad Request"
            alert Constants.errors.general
          when "Forbidden"
            alert Constants.errors.forbidden

      $.ajax
        url: "//<%= DOMAIN %>/#{method}.json"
        dataType: "jsonp"
        data: data
        complete: complete
        error: error
        success: (data, textStatus, jqXHR) ->
          if data.meta.status < 400
            success Backbone.Model::factory(data.response)
          else
            jqXHR.status = data.meta.status
            jqXHR.responseText = data
            error jqXHR, textStatus, data.meta.msg

  #-----------------------------------------
  # Submit the post

  reading.submit = submit = (params) ->
    post = Post::current = new Post
    post.save params, success: (data) ->
      if platform is "redirect"
        # forward back through to Reading so that the user's token doesn't show up in the referrer
        window.location = if window.location.href.indexOf('/t/') > -1 then "http://<%= DOMAIN %>/t/-/#{params.url}" else params.url

    AppView::current.close() if AppView::current?
    AppView::current = new AppView model: post if platform isnt "redirect"

  #-----------------------------------------
  # Initialize!

  init = ->
    # fire an event to let people know reading is ready
    # can't seem to use jquery.trigger for events attached
    # without jquery
    e = document.createEvent "Event"
    e.initEvent "reading.ready", true, true
    document.dispatchEvent e
    reading.ready = true

    win_focus = true
    $(window).focus(-> win_focus = true).blur(-> win_focus = false)
    $.fn.hasFocus = -> win_focus

    if platform is "redirect" or platform is "bookmarklet"
      if platform is "redirect"
        url   = parse_url()
        title = null
        return window.location = url if token is "-" or !token
      else
        url   = window.location.href
        title = window.document.title

      submit
        url: url
        title: title
        referrer_id: reading.referrer_id ? 0


  #-----------------------------------------
  # Check the bookmarklet version

  up_to_date = ->
    !latest_versions[platform]? || String(version).replace(/\./g,'') >= String(latest_versions[platform]).replace(/\./g,'')

  #-----------------------------------------
  # Prompt to upgrade

  upgrade = -> $.getScript "//<%= DOMAIN %>/assets/bookmarklet/upgrade.js"

  #-----------------------------------------
  # Check for jQuery and initialize

  if up_to_date() then init() else upgrade()
